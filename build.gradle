plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.2'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.odin568'

// GitHub-Version >> Docker Version >> Fallback
if (System.getenv("RELEASE_VERSION")) {
	version = System.getenv("RELEASE_VERSION")
}
else if (project.hasProperty("RELEASE_VERSION") && project.getProperty("RELEASE_VERSION") && !project.getProperty("RELEASE_VERSION").allWhitespace) {
	version = project.getProperty("RELEASE_VERSION")
}
else {
	version = "SNAPSHOT"
}
println("Building version $version")

// Omit version number in artifacts for simpler releases
bootJar.archiveFileName = "coachbetter-calendar-events.jar"

jar {
	enabled = false
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	implementation group: 'org.json', name: 'json', version: '20250107'
	implementation group: 'org.mnode.ical4j', name: 'ical4j', version: '4.1.0'
	implementation group: 'com.auth0', name: 'java-jwt', version: '4.4.0'

	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
}

springBoot {
	buildInfo()
}
